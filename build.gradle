plugins {
    id 'scala'
    id 'maven-publish'
    id 'com.github.maiflai.scalatest' version '0.26'
}

group 'fr.il_totore'
version '1.0-SNAPSHOT'


ext {
    scalaVersion = '2.13.1'
}


repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation scalaLib()
    implementation scalaModule('scala-collection-contrib', '0.2.1')
    testImplementation scalaDependency('org.scalatest', 'scalatest', '3.1.1')
}

compileScala.options.encoding = compileJava.options.encoding = compileTestScala.options.encoding = compileTestJava.options.encoding = 'UTF-8'


publishing {
    publications {
        mavenScala(MavenPublication) {
            groupId = 'io.github.iltotore'
            artifactId = 'ucp' + getMajorScalaVersion()
            version = '0.1'

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('implementation')
                }
            }

            repositories {
                maven {
                    url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials {
                        username = "Iltotore"
                        password = System.getenv("SONATYPE_PASSWORD")
                    }
                }
            }

            from components.java
        }
    }
}

def getMajorScalaVersion() {
    String[] parts = scalaVersion.tokenize('.')
    return parts[0] + '.' + parts[1]
}

def scalaModule(String name, String version) {
    return scalaDependency('org.scala-lang.modules', name, version)
}

def scalaDependency(String company, String name, String version) {
    return company + ':' + name + '_' + getMajorScalaVersion() + ':' + version
}

def scalaLib() {
    return 'org.scala-lang:scala-library:' + scalaVersion
}

task spec(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/scala/test', '-o']
    jvmArgs = ['-Dfile.encoding="UTF-8"']
    classpath = sourceSets.test.runtimeClasspath
    group 'verification'
}

test {
    enabled = false
    finalizedBy spec
}